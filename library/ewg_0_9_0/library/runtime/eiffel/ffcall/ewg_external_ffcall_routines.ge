indexing

	description:

		"External FFCALL routines"

	library: "Eiffel Wrapper Generator Library"
	copyright: "Copyright (c) 1999, Andreas Leitner and others"
	license: "Eiffel Forum License v2 (see forum.txt)"
	date: "$Date: 2004/02/27 12:25:54 $"
	revision: "$Revision: 1.1 $"

class EWG_EXTERNAL_FFCALL_ROUTINES

feature {ANY}

	alloc_callback_external (a_function: POINTER; a_data: POINTER): POINTER is
		external
#ifdef SE || ISE
			"C inline use <callback.h>"
		alias
			"alloc_callback ($a_function, $a_data)"
#endif		
#ifdef VE
			"C"
		alias
			"ewg_ffcall_alloc_callback"
#endif
		end
 
	free_callback_external (a_function: POINTER) is
		external
#ifdef SE || ISE
			"C inline use <callback.h>"
		alias
			"free_callback ($a_function)"
#endif
#ifdef VE
			"C"
		alias
			"ewg_ffcall_free_callback"
#endif
		end

	

	make_copy_from_c_zero_terminated_string (a_c_string: POINTER): STRING is
			-- Create a new Eiffel string and copy the contents of
			-- `a_c_string' into it. `a_c_string' must point to a
			-- c zero terminated string.
		require
			a_c_string_not_void: a_c_string /= Void
			a_c_string_not_default: a_c_string /= Default_pointer
		do
#ifdef ISE
				create Result.make_from_c (a_c_string)
#endif
#ifdef VE
				create Result.make (strlen_external (a_c_string))
				Result.from_c (a_c_string)
#endif
#ifdef SE
				create Result.from_external_copy (a_c_string)
#endif
		ensure
			result_not_void: Result /= Void
		end

feature

	string_to_pointer (a_string: STRING): POINTER is
			-- Get a c pointer to the storage area of an Eiffel string
			-- Thanks to Berend resp. the mico/e team (;
			-- Note: This is extremly dangerous buisness
		require
			a_string_not_void: a_string /= Void
#ifdef ISE
		local
			a: ANY
			pr: POINTER_REF
#endif
		do
#ifdef ISE
			a := a_string.to_c
			create pr
			pr.set_item ($a)
			Result := pr.item
#endif
#ifdef SE || VE
				Result := a_string.to_external
#endif
		ensure
			result_not_void: Result /= Void
			result_not_default_pointer: Result /= default_pointer
		end


	strlen_external (ptr: POINTER): INTEGER is
		require
			ptr_not_nil: ptr /= default_pointer
		external
#ifdef ISE
			"C (char *): EIF_INTEGER | <string.h>"
#endif
#ifdef SE
			"C macro use <string.h>"
#endif
#ifdef VE
			"C"
#endif
		alias
			"strlen"
		end

	strcpy_external (a_dest, a_src: POINTER): POINTER is
		require
			a_dest_not_default: a_dest /= Default_pointer
			a_src_not_default: a_src /= Default_pointer
		external
#ifdef ISE
			"C (char *, char *): EIF_POINTER | <string.h>"
#endif
#ifdef SE
			"C macro use <string.h>"
#endif
#ifdef VE
			"C"
#endif
		alias
			"strcpy"
		end


end
